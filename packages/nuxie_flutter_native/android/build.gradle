group = "io.nuxie.flutter.nativeplugin"
version = "1.0-SNAPSHOT"

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterSdkPath = localProperties.getProperty("flutter.sdk") ?: System.getenv("FLUTTER_ROOT")
def flutterJarCandidates = []
if (flutterSdkPath != null) {
    ["android-arm64", "android-arm", "android-x64"].each { arch ->
        def flutterJar = file("${flutterSdkPath}/bin/cache/artifacts/engine/${arch}/flutter.jar")
        if (flutterJar.exists()) {
            flutterJarCandidates.add(flutterJar)
        }
    }
}

buildscript {
    ext.kotlin_version = "2.0.21"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.6.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "io.nuxie.flutter.nativeplugin"
    compileSdk = 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
    }
}

dependencies {
    if (!flutterJarCandidates.isEmpty()) {
        compileOnly(files(flutterJarCandidates))
    }

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3")

    if (project.findProject(":nuxie-android") != null) {
        implementation(project(":nuxie-android"))
    }
}
